
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						WPO - STYLE CHANGES
//__________________________________________________________________________________
//__________________________________________________________________________________


//UPDATE STYLE STRINGS______________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
	SET_2DA_ENTRY 31 2 1 RESOLVE_STR_REF (@1250) // 2HS
	SET_2DA_ENTRY 31 3 1 RESOLVE_STR_REF (@1245) // 2HS
	SET_2DA_ENTRY 32 2 1 RESOLVE_STR_REF (@1251) // SnS
	SET_2DA_ENTRY 32 3 1 RESOLVE_STR_REF (@1246) // SnS
	SET_2DA_ENTRY 33 2 1 RESOLVE_STR_REF (@1252) // SWS
	SET_2DA_ENTRY 33 3 1 RESOLVE_STR_REF (@1247) // SWS
	SET_2DA_ENTRY 34 2 1 RESOLVE_STR_REF (@1253) // DW
	SET_2DA_ENTRY 34 3 1 RESOLVE_STR_REF (@1249) // DW
BUT_ONLY
//__________________________________________________________________________________


//AMEND WEAPPROF.2da________________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
	// Fighter
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 5 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 5 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 5 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 5 2
	// Cleric
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 6 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 6 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 6 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 6 0
	// Thief
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 7 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 7 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 7 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 7 2
	// Bard
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 8 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 8 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 8 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 8 2
	// Paladin
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 9 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 9 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 9 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 9 2
	// Druid
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 10 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 10 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 10 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 10 2
	// Ranger
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 11 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 11 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 11 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 11 2
	// Fighter/Mage
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 12 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 12 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 12 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 12 0
	// Fighter/Cleric
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 13 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 13 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 13 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 13 0
	// Fighter/Thief
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 14 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 14 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 14 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 15 2
	// Fighter/Mage/Thief
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 15 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 15 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 15 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 15 2
	// Mage/Thief
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 16 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 16 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 16 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 16 2
	// Cleric/Mage
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 17 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 17 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 17 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 17 0
	// Cleric/Thief
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 18 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 18 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 18 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 18 2
	// Fighter/Druid
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 19 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 19 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 19 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 19 2
	// Fighter/Mage/Cleric
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 20 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 20 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 20 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 20 0
	// Ranger/Cleric
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 21 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 21 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 21 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 21 2
	// Wizards
	// Berserker
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 30 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 30 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 30 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 30 2
	// Wizard Slayer
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 31 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 31 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 31 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 31 2
	// Kensai
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 32 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 32 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 32 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 32 2
	// Cavalier
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 33 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 33 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 33 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 33 2
	// Inquisitor
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 34 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 34 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 34 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 34 2
	// Undead Hunter
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 35 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 35 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 35 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 35 2
	// Archer
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 36 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 36 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 36 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 36 2
	// Stalker
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 37 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 37 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 37 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 37 2
	// Beastmaster
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 38 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 38 2
	// Assassin
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 39 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 39 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 39 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 39 2
	// Bounty Hunter
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 40 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 40 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 40 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 40 2
	// Swashbuckler
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 41 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 41 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 41 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 41 2
	// Blade
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 42 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 42 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 42 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 42 2
	// Jester
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 43 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 43 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 43 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 43 2
	// Skald
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 44 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 44 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 44 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 44 2
	// Totemic Driud
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 45 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 45 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 45 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 45 2
	// Shapeshifter
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 46 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 46 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 46 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 46 2
	// Avenger
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 47 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 47 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 47 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 47 2
	// Talos
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 48 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 48 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 48 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 48 0
	// Helm
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 49 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 49 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 49 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 49 0
	// Lathander
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 49 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 49 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 49 1
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 49 0
	// Monk
	// Barbarian
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 52 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 52 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 52 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 52 2
	// Wild Mage	
	SET_2DA_ENTRIES_NOW ~#weapprof~ 1

ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	COPY_EXISTING ~weapprof.2da~ ~override~
		// Blackguard
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 31 54 2
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 32 54 2
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 33 54 2
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 34 54 2
		// Shadowdancer
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 31 55 0
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 32 55 0
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 33 55 2
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 34 55 2
		// Dwarven Defender
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 31 56 2
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 32 56 2
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 33 56 0
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 34 56 2
		// Dragon Disciple
		// Dark Moon Monk
		// Sun Soul Monk
		// Tyr
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 31 61 1
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 32 61 1
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 33 61 1
		SET_2DA_ENTRY_LATER ~EE#weapprof~ 34 61 0
		SET_2DA_ENTRIES_NOW ~EE#weapprof~ 1
END
	
ACTION_IF GAME_IS ~bgee~ THEN BEGIN
	COPY_EXISTING ~weapprof.2da~ ~override~
		// Shaman
		SET_2DA_ENTRY_LATER ~BEE#weapprof~ 31 60 1
		SET_2DA_ENTRY_LATER ~BEE#weapprof~ 32 60 0
		SET_2DA_ENTRY_LATER ~BEE#weapprof~ 33 60 1
		SET_2DA_ENTRY_LATER ~BEE#weapprof~ 34 60 2
	SET_2DA_ENTRIES_NOW ~BEE#weapprof~ 1
END
ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
	COPY_EXISTING ~weapprof.2da~ ~override~
		// Shaman
		SET_2DA_ENTRY_LATER ~2EE#weapprof~ 31 62 1
		SET_2DA_ENTRY_LATER ~2EE#weapprof~ 32 62 0
		SET_2DA_ENTRY_LATER ~2EE#weapprof~ 33 62 1
		SET_2DA_ENTRY_LATER ~2EE#weapprof~ 34 62 2
	SET_2DA_ENTRIES_NOW ~2EE#weapprof~ 1
END

ACTION_IF GAME_IS ~iwdee~ THEN BEGIN
	COPY_EXISTING ~weapprof.2da~ ~override~
		// Tempus
		SET_2DA_ENTRY_LATER ~IEE#weapprof~ 31 62 2
		SET_2DA_ENTRY_LATER ~IEE#weapprof~ 32 62 2
		SET_2DA_ENTRY_LATER ~IEE#weapprof~ 33 62 2
		SET_2DA_ENTRY_LATER ~IEE#weapprof~ 34 62 2
		SET_2DA_ENTRIES_NOW ~IEE#weapprof~ 1
END
//__________________________________________________________________________________


//STYLES FOR MOD CLERICS____________________________________________________________
//
	OUTER_SET count = 0
	ACTION_FOR_EACH class IN
                  cleric
	BEGIN
		OUTER_SPRINT $clerics("%count%") "%class%"
		OUTER_SET ++count
	END
	COPY_EXISTING kitlist.2da override
		READ_2DA_ENTRIES_NOW kitlist 3
		FOR (row = 31; row < kitlist; ++row) BEGIN // Skip past the vanilla kits
			READ_2DA_ENTRY_FORMER kitlist row 1 label
			READ_2DA_ENTRY_FORMER kitlist row 8 class
			PATCH_IF class = 3 BEGIN // cleric
				SPRINT $clerics("%count%") "%label%"
				++count
			END
		END
	BUT_ONLY
	COPY_EXISTING ~WEAPPROF.2DA~ override
		READ_2DA_ENTRIES_NOW weapprof 3
		COUNT_2DA_COLS cols
		PHP_EACH clerics AS _ => name BEGIN
			FOR (col = 0; col < cols - 1; ++col) BEGIN
				READ_2DA_ENTRY_FORMER weapprof 0 col column_name
				PATCH_IF "%column_name%" STRING_EQUAL_CASE "%name%" BEGIN
					SET_2DA_ENTRY_LATER C#weapprof 31 (col + 1) 1
					SET_2DA_ENTRY_LATER C#weapprof 32 (col + 1) 1
					SET_2DA_ENTRY_LATER C#weapprof 33 (col + 1) 1
					SET_2DA_ENTRY_LATER C#weapprof 34 (col + 1) 0
				END
			END
		END
		SET_2DA_ENTRIES_NOW C#weapprof 1
		PRETTY_PRINT_2DA
	BUT_ONLY
//__________________________________________________________________________________


//STYLES FOR MOD DRUIDS_____________________________________________________________
//
	OUTER_SET count = 0
	ACTION_FOR_EACH class IN
                  druid
	BEGIN
		OUTER_SPRINT $druids("%count%") "%class%"
		OUTER_SET ++count
	END
	COPY_EXISTING kitlist.2da override
		READ_2DA_ENTRIES_NOW kitlist 3
		FOR (row = 31; row < kitlist; ++row) BEGIN // Skip past the vanilla kits
			READ_2DA_ENTRY_FORMER kitlist row 1 label
			READ_2DA_ENTRY_FORMER kitlist row 8 class
			PATCH_IF class = 11 BEGIN // druid
				SPRINT $druids("%count%") "%label%"
				++count
			END
		END
	BUT_ONLY
	COPY_EXISTING ~WEAPPROF.2DA~ override
		READ_2DA_ENTRIES_NOW weapprof 3
		COUNT_2DA_COLS cols
		PHP_EACH druids AS _ => name BEGIN
			FOR (col = 0; col < cols - 1; ++col) BEGIN
				READ_2DA_ENTRY_FORMER weapprof 0 col column_name
				PATCH_IF "%column_name%" STRING_EQUAL_CASE "%name%" BEGIN
					SET_2DA_ENTRY_LATER D#weapprof 31 (col + 1) 2
					SET_2DA_ENTRY_LATER D#weapprof 32 (col + 1) 0
					SET_2DA_ENTRY_LATER D#weapprof 33 (col + 1) 2
					SET_2DA_ENTRY_LATER D#weapprof 34 (col + 1) 2
				END
			END
		END
		SET_2DA_ENTRIES_NOW D#weapprof 1
		PRETTY_PRINT_2DA
	BUT_ONLY
//__________________________________________________________________________________


//STYLES FOR MOD THIEVES____________________________________________________________
//
	OUTER_SET count = 0
	ACTION_FOR_EACH class IN
                  thief
	BEGIN
		OUTER_SPRINT $thiefs("%count%") "%class%"
		OUTER_SET ++count
	END
	COPY_EXISTING kitlist.2da override
		READ_2DA_ENTRIES_NOW kitlist 3
		FOR (row = 31; row < kitlist; ++row) BEGIN // Skip past the vanilla kits
			READ_2DA_ENTRY_FORMER kitlist row 1 label
			READ_2DA_ENTRY_FORMER kitlist row 8 class
			PATCH_IF class = 4 BEGIN // thief
				SPRINT $thiefs("%count%") "%label%"
				++count
			END
		END
	BUT_ONLY
	COPY_EXISTING ~WEAPPROF.2DA~ override
		READ_2DA_ENTRIES_NOW weapprof 3
		COUNT_2DA_COLS cols
		PHP_EACH thiefs AS _ => name BEGIN
			FOR (col = 0; col < cols - 1; ++col) BEGIN
				READ_2DA_ENTRY_FORMER weapprof 0 col column_name
				PATCH_IF "%column_name%" STRING_EQUAL_CASE "%name%" BEGIN
					SET_2DA_ENTRY_LATER T#weapprof 31 (col + 1) 0
					SET_2DA_ENTRY_LATER T#weapprof 32 (col + 1) 0
					SET_2DA_ENTRY_LATER T#weapprof 33 (col + 1) 2
					SET_2DA_ENTRY_LATER T#weapprof 34 (col + 1) 2
				END
			END
		END
		SET_2DA_ENTRIES_NOW T#weapprof 1
		PRETTY_PRINT_2DA
	BUT_ONLY
//__________________________________________________________________________________


//STYLES FOR MOD BARDS______________________________________________________________
//
	OUTER_SET count = 0
	ACTION_FOR_EACH class IN
                  bard
	BEGIN
		OUTER_SPRINT $bards("%count%") "%class%"
		OUTER_SET ++count
	END
	COPY_EXISTING kitlist.2da override
		READ_2DA_ENTRIES_NOW kitlist 3
		FOR (row = 31; row < kitlist; ++row) BEGIN // Skip past the vanilla kits
			READ_2DA_ENTRY_FORMER kitlist row 1 label
			READ_2DA_ENTRY_FORMER kitlist row 8 class
			PATCH_IF class = 5 BEGIN // bard
				SPRINT $bards("%count%") "%label%"
				++count
			END
		END
	BUT_ONLY
	COPY_EXISTING ~WEAPPROF.2DA~ override
		READ_2DA_ENTRIES_NOW weapprof 3
		COUNT_2DA_COLS cols
		PHP_EACH bards AS _ => name BEGIN
			FOR (col = 0; col < cols - 1; ++col) BEGIN
				READ_2DA_ENTRY_FORMER weapprof 0 col column_name
				PATCH_IF "%column_name%" STRING_EQUAL_CASE "%name%" BEGIN
					SET_2DA_ENTRY_LATER B#weapprof 31 (col + 1) 2
					SET_2DA_ENTRY_LATER B#weapprof 32 (col + 1) 2
					SET_2DA_ENTRY_LATER B#weapprof 33 (col + 1) 2
					SET_2DA_ENTRY_LATER B#weapprof 34 (col + 1) 2
				END
			END
		END
		SET_2DA_ENTRIES_NOW B#weapprof 1
		PRETTY_PRINT_2DA
	BUT_ONLY
//__________________________________________________________________________________


//STYLES FOR MOD RANGERS____________________________________________________________
//
	OUTER_SET count = 0
	ACTION_FOR_EACH class IN
                  ranger
	BEGIN
		OUTER_SPRINT $rangers("%count%") "%class%"
		OUTER_SET ++count
	END
	COPY_EXISTING kitlist.2da override
		READ_2DA_ENTRIES_NOW kitlist 3
		FOR (row = 31; row < kitlist; ++row) BEGIN // Skip past the vanilla kits
			READ_2DA_ENTRY_FORMER kitlist row 1 label
			READ_2DA_ENTRY_FORMER kitlist row 8 class
			PATCH_IF class = 12 BEGIN // ranger
				SPRINT $rangers("%count%") "%label%"
				++count
			END
		END
	BUT_ONLY
	COPY_EXISTING ~WEAPPROF.2DA~ override
		READ_2DA_ENTRIES_NOW weapprof 3
		COUNT_2DA_COLS cols
		PHP_EACH rangers AS _ => name BEGIN
			FOR (col = 0; col < cols - 1; ++col) BEGIN
				READ_2DA_ENTRY_FORMER weapprof 0 col column_name
				PATCH_IF "%column_name%" STRING_EQUAL_CASE "%name%" BEGIN
					SET_2DA_ENTRY_LATER R#weapprof 31 (col + 1) 2
					SET_2DA_ENTRY_LATER R#weapprof 32 (col + 1) 0
					SET_2DA_ENTRY_LATER R#weapprof 33 (col + 1) 2
					SET_2DA_ENTRY_LATER R#weapprof 34 (col + 1) 2
				END
			END
		END
		SET_2DA_ENTRIES_NOW R#weapprof 1
		PRETTY_PRINT_2DA
	BUT_ONLY
//__________________________________________________________________________________


//STYLES FOR MOD PALADINS___________________________________________________________
//
	OUTER_SET count = 0
	ACTION_FOR_EACH class IN
                  paladin
	BEGIN
		OUTER_SPRINT $paladins("%count%") "%class%"
		OUTER_SET ++count
	END
	COPY_EXISTING kitlist.2da override
		READ_2DA_ENTRIES_NOW kitlist 3
		FOR (row = 31; row < kitlist; ++row) BEGIN // Skip past the vanilla kits
			READ_2DA_ENTRY_FORMER kitlist row 1 label
			READ_2DA_ENTRY_FORMER kitlist row 8 class
			PATCH_IF class = 6 BEGIN // paladin
				SPRINT $paladins("%count%") "%label%"
				++count
			END
		END
	BUT_ONLY
	COPY_EXISTING ~WEAPPROF.2DA~ override
		READ_2DA_ENTRIES_NOW weapprof 3
		COUNT_2DA_COLS cols
		PHP_EACH paladins AS _ => name BEGIN
			FOR (col = 0; col < cols - 1; ++col) BEGIN
				READ_2DA_ENTRY_FORMER weapprof 0 col column_name
				PATCH_IF "%column_name%" STRING_EQUAL_CASE "%name%" BEGIN
					SET_2DA_ENTRY_LATER P#weapprof 31 (col + 1) 2
					SET_2DA_ENTRY_LATER P#weapprof 32 (col + 1) 2
					SET_2DA_ENTRY_LATER P#weapprof 33 (col + 1) 2
					SET_2DA_ENTRY_LATER P#weapprof 34 (col + 1) 2
				END
			END
		END
		SET_2DA_ENTRIES_NOW P#weapprof 1
		PRETTY_PRINT_2DA
	BUT_ONLY
//__________________________________________________________________________________


//CHANGE BONUSES____________________________________________________________________
//
/*
ACTION_IF (FILE_EXISTS_IN_GAME ~d5_dwweap.d5~) BEGIN	//	if light/heavy weapon distinctions installed
	COPY ~scales_of_balance/profs/stylbonu_iwo.2da~ ~override/stylbonu.2da~
END
*/
//ACTION_IF NOT (FILE_EXISTS_IN_GAME ~d5_dwweap.d5~) BEGIN
	COPY ~scales_of_balance/profs/stylbonu.2da~ ~override~
//END
//__________________________________________________________________________________


//CHANGE LIGHT WEAPON BONUSES TO MAIN_HAND__________________________________________
//
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
	PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
		READ_BYTE ~0x31~ ~prof~
		PATCH_IF (~%prof%~ = ~96~) OR 		// dagger
				(~%prof%~ = ~91~) OR 		// short sword
				(~%prof%~ = ~115~) BEGIN 	// club
			LPF ALTER_EFFECT INT_VAR match_opcode = 305 match_parameter1 = 1 opcode = 306 END
		END
	END
BUT_ONLY
//__________________________________________________________________________________


//SWS APR___________________________________________________________________________
//
/*
TRYING AGAIN

1-hand items:
- while equipped, increment 134 by 32
- on equip, cancel any APR bonus from SWS
- on equip, conditionally cast spellB if not 64 pips in 134
- while equipped, trigger .eff to run 232 effect

.eff:
- every 6 seconds, cast spellA

spellA:
- conditionally cast spellB if 32 pips 134

spellB:
- conditionally cast spellC if 2 pips sws

spellC: 
- +0.5 APR, 7 seconds

spellZ:
- cancel spellC

2-hand items:
- on equip, cancel spellC
*/

APPEND ~splprot.2da~ ~D5_SPEC_SWS%TAB%113%TAB%2%TAB%4~		//	specialized or better in SWS
APPEND ~splprot.2da~ ~D5_134_BIT6%TAB%134%TAB%50%TAB%8~		//	50 = 32 = bit6 of extraproficiency20
APPEND ~splprot.2da~ ~D5_134_BIT7%TAB%134%TAB%100%TAB%8~	//	100 = 64 = bit7 of extraproficiency20
APPEND ~splprot.2da~ ~D5_134_NOT7%TAB%134%TAB%100%TAB%9~	//	100 = 64 = bit7 of extraproficiency20

COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~ // read column value
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_SPEC_SWS~ BEGIN
	    SET sws_spec = %row%
	  END
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_134_BIT6~ BEGIN
	    SET 134_bit6 = %row%
	  END
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_134_BIT7~ BEGIN
	    SET 134_bit7 = %row%
	  END
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_134_NOT7~ BEGIN
	    SET 134_not7 = %row%
	  END
	END
BUT_ONLY

COPY ~scales_of_balance/profs/d5_sws.eff~ ~override~
COPY ~scales_of_balance/profs/d5_sws1.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter1 = 0 parameter2 = %134_bit6% END
COPY ~scales_of_balance/profs/d5_sws2.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter1 = 0 parameter2 = %sws_spec% END
COPY ~scales_of_balance/profs/d5_sws3.spl~ ~override~
COPY ~scales_of_balance/profs/d5_swsx.spl~ ~override~
COPY ~scales_of_balance/profs/d5_swsz.spl~ ~override~
COPY ~scales_of_balance/profs/d5_swsn.spl~ ~override~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
	READ_SHORT 0x1c type
	READ_BYTE 0x31 prof
	READ_BYTE 0x18 handed
	PATCH_IF (%prof% = 92) OR	// axe
			(%prof% = 89) OR	// bastard sword
			(%prof% = 115) OR	// club
			(%prof% = 96) OR	// dagger
			(%prof% = 100) OR	// flail
			(%prof% = 94) OR	// katana
			(%prof% = 90) OR	// long sword
			(%prof% = 101) OR	// mace
			(%prof% = 95) OR	// scimitar
			(%prof% = 91) OR	// short sword
			(%prof% = 97) BEGIN	 // hammer
	  PATCH_IF ((%handed% BOR 0b11111101) = 0b11111101) BEGIN 	//	1-handed
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 2 STR_VAR resource = ~d5_sws~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 326 target = 1 parameter1 = 0 parameter2 = %134_not7% timing = 1 STR_VAR resource = ~d5_sws2~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_sws3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 233 target = 1 parameter1 = 32 parameter2 = (134 + (0x10000 * 1)) timing = 2 END
	  END
	END

	PATCH_IF ((%handed% BAND 0b00000010) = 0b00000010) BEGIN 	//	2-handed
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_sws3~ END
	END

	PATCH_IF (%prof% = 106) OR	// darts
			(%type% = 27) OR	// crossbows
			(%type% = 15) OR	// bows
			(%type% = 18) BEGIN	// slings
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_sws3~ END
	END

	PATCH_IF (%prof% = 96) BEGIN	// daggers (and maybe darts)
	  READ_LONG 0x64 abil_offset
	  READ_SHORT 0x68 abil_number
	  WHILE (%abil_number% > 0) BEGIN
		SET abil_number = (%abil_number% - 1)
		READ_SHORT (%abil_offset% + (0x38 * %abil_number%)) range
		PATCH_IF (%range% = 2 || %range% = 4) BEGIN 	// ranged weapon
		  LPF ALTER_EFFECT INT_VAR match_opcode = 1 match_parameter1 = 2 match_parameter2 = 1 parameter1 = 7 END
		  LPF ALTER_EFFECT INT_VAR match_opcode = 1 match_parameter1 = 3 match_parameter2 = 1 parameter1 = 8 END
		END
	  END
	END

	PATCH_IF (%type% = 12) BEGIN	//	shields
//	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 parameter1 = (0 - 1) timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_sws1~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_sws3~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 233 target = 1 parameter1 = 32 parameter2 = (134 + (0x10000 * 1)) timing = 2 END
	END
  END
BUT_ONLY

ACTION_FOR_EACH class IN ~clabfi01~ ~clabrn01~ ~clabth01~ ~clabpr01~ ~clabdr01~ ~clabma01~ BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%class%.2da~ BEGIN
	APPEND ~%class%.2da~ ~ABILITY    AP_D5_SWSN  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****  ~ 
  END
END

//__________________________________________________________________________________


//SHIELD BASH_______________________________________________________________________
//
APPEND ~splprot.2da~ ~D5_SPEC_SNS%TAB%112%TAB%2%TAB%4~
APPEND ~splprot.2da~ ~D5_134_BIT4%TAB%134%TAB%8%TAB%8~
APPEND ~splprot.2da~ ~D5_134_NOT4%TAB%134%TAB%8%TAB%9~

COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~ // read column value
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_SPEC_SNS~ BEGIN
	    SET sns_spec = %row%
	  END
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_134_BIT4~ BEGIN
	    SET 134_bit4 = %row%
	  END
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_134_NOT4~ BEGIN
	    SET 134_not4 = %row%
	  END
	END
BUT_ONLY

COPY ~scales_of_balance/profs/d5_sns1.spl~ ~override~	
	LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter2 = %sns_spec% END
COPY ~scales_of_balance/profs/d5_sns1a.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter1 = 8 parameter2 = %134_bit4% END
	LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 326 parameter1 = 8 parameter2 = %134_not4% STR_VAR resource = ~d5_snsn~ END
COPY ~scales_of_balance/profs/d5_sns2.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter2 = %sns_spec% END
COPY ~scales_of_balance/profs/d5_sns2a.spl~ ~override~
COPY ~scales_of_balance/profs/d5_sns2b.spl~ ~override~
COPY ~scales_of_balance/profs/d5_snsn.spl~ ~override~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
	READ_SHORT 0x1c type
	PATCH_IF (%type% = 12) BEGIN
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 2 resist_dispel = 0 probability1 = 100 STR_VAR resource = ~d5_sns1~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 2 resist_dispel = 0 probability1 = 100 STR_VAR resource = ~d5_sns2~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 233 target = 1 parameter1 = 8 parameter2 = (134 + (0x10000 * 1)) timing = 2 resist_dispel = 0 END
	END
  END
BUT_ONLY

//__________________________________________________________________________________


CLEAR_ARRAYS
