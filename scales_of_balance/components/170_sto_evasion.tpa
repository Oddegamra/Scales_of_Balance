
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//							IWD SPELL EVASION
//__________________________________________________________________________________
//__________________________________________________________________________________


//COPY MARKER FILE_________________________________________________________________
//
COPY_EXISTING ~sw1h04.itm~ ~override/d5_evasion.d5~
//__________________________________________________________________________________


//define function___________________________________________________________________
//
INCLUDE ~%MOD_FOLDER%/lib/spell_evasion/spell_evasion.tpa~
//__________________________________________________________________________________


//spell to set evasion state________________________________________________________
//
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5evade.spl~ BEGIN
  COPY_EXISTING ~spcl221.spl~ ~override/d5evade.spl~
	SAY NAME1 ~ ~
	SAY UNIDENTIFIED_DESC ~ ~
	LPF DELETE_EFFECT INT_VAR match_probability1 = 100 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 1 parameter2 = 252 timing = 9 special = 1 END
  BUT_ONLY
END
//__________________________________________________________________________________


//patch spells for evasion__________________________________________________________
//
ACTION_PHP_EACH evade_spells AS evadable_spell => ind BEGIN 
  LAF add_evade_spell INT_VAR evasion_spellstate = 252 evasion_save = 2 STR_VAR evade_spell = EVAL ~%evadable_spell%~ evade_prefix = ~d5ev~ END
END
//__________________________________________________________________________________


//ADD EVASION FOR THIEVES___________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~clabth01.2da~ BEGIN
  COPY_EXISTING ~clabth01.2da~ ~override~
    APPEND ~clabth01.2da~ ~EVASION     ****        ****        ****        ****        ****        ****        AP_D5EVADE  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        **** ~ UNLESS ~AP_D5EVADE~
  BUT_ONLY
END

COPY_EXISTING ~kitlist.2da~ ~override~
//  COUNT_2DA_COLS cols
  READ_2DA_ENTRIES_NOW ~r2en_kitlist~ 9
  FOR (row = 2; row < r2en_kitlist; row += 1) BEGIN
    READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 5 class_ab
    READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 8 class_num
    PATCH_IF (class_num == 4) BEGIN // thieves
      INNER_ACTION BEGIN
        APPEND ~%class_ab%.2da~ ~EVASION     ****        ****        ****        ****        ****        ****        AP_D5EVADE  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        **** ~ UNLESS ~AP_D5EVADE~
      END
    END
  END
BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~d5psirog.2da~ BEGIN
  COPY_EXISTING ~d5psirog.2da~ ~override~
    REPLACE_TEXTUALLY ~AP_D5EVADE  ~ ~****        ~
  BUT_ONLY
END
//___________________________________________________________________________________


//ADD EVASION TO ENEMY THIEVES_______________________________________________________
//
LAM JOINABLE_NPC_ARRAYS

ACTION_PHP_EACH NON_JOINABLE_NPC_ARRAY AS cre => dv BEGIN
//  PRINT ~%cre% => %dv%~ 
  COPY_EXISTING ~%cre%~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
	  READ_BYTE 0x234 level_1
	  READ_BYTE 0x235 level_2
	  READ_BYTE 0x273 npc_class
	  PATCH_IF (npc_class = 4) OR (npc_class = 9) OR (npc_class = 13) OR (npc_class = 15) BEGIN
		PATCH_IF (level_1 > 6) OR (level_2 > 6) BEGIN
		  LPF ADD_CRE_EFFECT INT_VAR opcode = 328 target = 1 parameter2 = 252 timing = 9 special = 1 END
		END
	  END
	END
  BUT_ONLY
END
//___________________________________________________________________________________

