
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						MAGIC RESISTANCE OVERHAUL
//__________________________________________________________________________________
//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION revise_poisons BEGIN

// find all poison effects
// the *best* ones should do like 2hp/sec
// maybe need to impose structure?
// - weakest: 1hp/round
// - weak: 1hp/3 seconds
// - moderate: 1hp/2 seconds
// - strong: 1hp/second
// - strongest: 2hp/second
// durations should be extended, though

// maybe set those up as .effs, and have all poison effects use one .eff
// then can actually do something with Slow Poison and Neutralize Poison!
// maybe
// question becomes, how to fit all existing poison effects into the above structure?
// and what about duration?

// how about adding disease/stat drain? would make sense...

// now need to check strength and duration of each and add the category to the array

INCLUDE ~%MOD_FOLDER%/lib/misc_functions.tpa~

// special handling for RR#DIMP
// change to a moderate poison

OUTER_SPRINT $poison_effects(~RES~ ~type~) ~magnitude~

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
 PATCH_IF (%SOURCE_SIZE% > 0x71) BEGIN
  SET poison_effect = 0
  SPRINT magnitude ~meh~
  GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
  PHP_EACH ab_array AS int => ab_off BEGIN
	GET_OFFSET_ARRAY2 fx_array ab_off SPL_V10_HEAD_EFFECTS
	PHP_EACH fx_array AS int => fx_off BEGIN
	  READ_SHORT fx_off fx_type
	  PATCH_IF fx_type = 25 BEGIN 							// poison
		SET poison_effect = 1
		READ_LONG (fx_off + 0x04) fx_param1
		READ_LONG (fx_off + 0x08) fx_param2
		READ_LONG (fx_off + 0x12) fx_duration
		PATCH_IF (fx_param2 = 2) && (fx_param1 > 3) BEGIN
		  SPRINT magnitude ~LETHAL~
		END
		PATCH_IF (fx_param2 = 2) && (fx_param1 > 1) && (fx_param1 < 4) BEGIN
		  SPRINT magnitude ~STRONG~
		END
		PATCH_IF (fx_param2 = 2) && (fx_param1 = 1) BEGIN
		  SPRINT magnitude ~MODERATE~
		END
		PATCH_IF (fx_param2 < 2) BEGIN
		  SPRINT magnitude ~MODERATE~
		END
		PATCH_IF (fx_param2 = 3) && (fx_param1 > 1) && (fx_param1 < 4) BEGIN
		  SPRINT magnitude ~WEAK~
		END
		PATCH_IF (fx_param2 = 3) && (fx_param1 > 3) BEGIN
		  SPRINT magnitude ~WEAKER~
		END
	  END
	  PATCH_IF fx_type = 12 BEGIN 							// damage (poison)
    	READ_SHORT fx_off + 0xa damage_type
		PATCH_IF (damage_type = 32) BEGIN
		  SET poison_effect = 1
		  PATCH_IF !(~%magnitude%~ STRING_EQUAL_CASE ~12_OVER_TIME~) BEGIN
			SPRINT magnitude ~12_INSTANT~
		  END
		  READ_LONG fx_off + 0x0c timing_mode
		  PATCH_IF (timing_mode = 4) BEGIN
			SPRINT magnitude ~12_OVER_TIME~
		  END
		END
	  END
	END
	PATCH_IF (poison_effect = 1) BEGIN
	  SPRINT $poison_effects(~%SOURCE_RES%~ ~SPL~) ~%magnitude%~
	END
  END
 END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
 PATCH_IF (%SOURCE_SIZE% > 0x71) BEGIN
  SET poison_effect = 0
  SPRINT magnitude ~meh~
  GET_OFFSET_ARRAY ab_array ITM_V10_HEADERS
  PHP_EACH ab_array AS int => ab_off BEGIN
	GET_OFFSET_ARRAY2 fx_array ab_off ITM_V10_HEAD_EFFECTS
	PHP_EACH fx_array AS int => fx_off BEGIN
	  READ_SHORT fx_off fx_type
	  PATCH_IF fx_type = 25 BEGIN 							// poison
		SET poison_effect = 1
		READ_LONG (fx_off + 0x04) fx_param1
		READ_LONG (fx_off + 0x08) fx_param2
		READ_LONG (fx_off + 0x12) fx_duration
		PATCH_IF (fx_param2 = 2) && (fx_param1 > 3) BEGIN
		  SPRINT magnitude ~LETHAL~
		END
		PATCH_IF (fx_param2 = 2) && (fx_param1 > 1) && (fx_param1 < 4) BEGIN
		  SPRINT magnitude ~STRONG~
		END
		PATCH_IF (fx_param2 = 2) && (fx_param1 = 1) BEGIN
		  SPRINT magnitude ~MODERATE~
		END
		PATCH_IF (fx_param2 < 2) BEGIN
		  SPRINT magnitude ~MODERATE~
		END
		PATCH_IF (fx_param2 = 3) && (fx_param1 = 1) BEGIN
		  SPRINT magnitude ~MODERATE~
		END
		PATCH_IF (fx_param2 = 3) && (fx_param1 > 1) && (fx_param1 < 4) BEGIN
		  SPRINT magnitude ~WEAK~
		END
		PATCH_IF (fx_param2 = 3) && (fx_param1 > 3) BEGIN
		  SPRINT magnitude ~WEAKER~
		END
	  END
	  PATCH_IF fx_type = 12 BEGIN 							// damage (poison)
    	READ_SHORT fx_off + 0xa damage_type
		PATCH_IF (damage_type = 32) BEGIN
		  SET poison_effect = 1
		  PATCH_IF !(~%magnitude%~ STRING_EQUAL_CASE ~12_OVER_TIME~) BEGIN
			SPRINT magnitude ~12_INSTANT~
		  END
		  READ_LONG fx_off + 0x0c timing_mode
		  PATCH_IF (timing_mode = 4) BEGIN
			SPRINT magnitude ~12_OVER_TIME~
		  END
		END
	  END
	END
	PATCH_IF (poison_effect = 1) BEGIN
	  SPRINT $poison_effects(~%SOURCE_RES%~ ~ITM~) ~%magnitude%~
	END
  END
 END
BUT_ONLY

OUTER_SPRINT $poison_effects(~RR#DIMP~ ~ITM~) ~MODERATE~

ACTION_PHP_EACH poison_effects AS res => mag BEGIN
  PRINT ~%res% is a %mag% poison %res_1%~
END

// make the slow poison spellstate
// or, just use stat 115, byte 4, bit 1

APPEND ~splprot.2da~ ~D5_SLOW_POISON%TAB%115%TAB%-1%TAB%8~ UNLESS ~D5_SLOW_POISON~
APPEND ~splprot.2da~ ~D5_NO_SLOW_POISON%TAB%115%TAB%-1%TAB%9~ UNLESS ~D5_NO_SLOW_POISON~

COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols 
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_SLOW_POISON~ BEGIN
	    SET slow_poison = %row%
	  END
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_NO_SLOW_POISON~ BEGIN
	    SET no_slow_poison = %row%
	  END
	END
BUT_ONLY

// effs for each poison strength

CREATE EFF ~d5poiz1~
	WRITE_LONG 0x10 232
	WRITE_LONG 0x14 2
	WRITE_LONG 0x1c 0
	WRITE_LONG 0x20 20
	WRITE_LONG 0x24 0
	WRITE_LONG 0x28 180
	WRITE_SHORT 0x2c 100
	WRITE_ASCII 0x30 ~d5poiz1~ #8
	WRITE_LONG 0x48 102

CREATE EFF ~d5poiz2~
	WRITE_LONG 0x10 232
	WRITE_LONG 0x14 2
	WRITE_LONG 0x1c 0
	WRITE_LONG 0x20 20
	WRITE_LONG 0x24 0
	WRITE_LONG 0x28 240
	WRITE_SHORT 0x2c 100
	WRITE_ASCII 0x30 ~d5poiz2~ #8
	WRITE_LONG 0x48 102

CREATE EFF ~d5poiz3~
	WRITE_LONG 0x10 232
	WRITE_LONG 0x14 2
	WRITE_LONG 0x1c 0
	WRITE_LONG 0x20 20
	WRITE_LONG 0x24 0
	WRITE_LONG 0x28 300
	WRITE_SHORT 0x2c 100
	WRITE_ASCII 0x30 ~d5poiz3~ #8
	WRITE_LONG 0x48 102

CREATE EFF ~d5poiz4~
	WRITE_LONG 0x10 232
	WRITE_LONG 0x14 2
	WRITE_LONG 0x1c 0
	WRITE_LONG 0x20 20
	WRITE_LONG 0x24 0
	WRITE_LONG 0x28 300
	WRITE_SHORT 0x2c 100
	WRITE_ASCII 0x30 ~d5poiz4~ #8
	WRITE_LONG 0x48 102

// make the 4 poison spells:
// - opcode 326, if no 'slow poison' spellstate, cast poison subspell
// - opcode 326, if 'slow poison' spellstate, cast 2 levels weaker subspell (or none if already weak)

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5poiz1.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 2 parameter1 = (1 << 24) parameter2 = %no_slow_poison% timing = 1 STR_VAR resource = ~d5poiz1a~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 2 parameter1 = (1 << 24) parameter2 = %slow_poison% timing = 1 STR_VAR resource = ~d5poiz3a~ END

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5poiz2.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 2 parameter1 = (1 << 24) parameter2 = %no_slow_poison% timing = 1 STR_VAR resource = ~d5poiz2a~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 2 parameter1 = (1 << 24) parameter2 = %slow_poison% timing = 1 STR_VAR resource = ~d5poiz4a~ END

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5poiz3.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 2 parameter1 = (1 << 24) parameter2 = %no_slow_poison% timing = 1 STR_VAR resource = ~d5poiz3a~ END

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5poiz4.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 2 parameter1 = (1 << 24) parameter2 = %no_slow_poison% timing = 1 STR_VAR resource = ~d5poiz4a~ END

// make the 4 subspells
// - opcode 25 with appropriate values, duration = 6 seconds
// - opcode 78, param1 = as appropriate (3-6?), param2 = 4
// - 206 vs. itself for 5 seconds

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5poiz2a.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 25 target = 1 parameter1 = 2 parameter2 = 2 timing = 0 duration = 7 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 78 target = 1 parameter1 = 6 parameter2 = 4 timing = 0 duration = 7 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 5 STR_VAR resource = ~d5poiz1a~ END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5poiz1a~ END

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5poiz2a.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 25 target = 1 parameter1 = 1 parameter2 = 2 timing = 0 duration = 7 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 78 target = 1 parameter1 = 5 parameter2 = 4 timing = 0 duration = 7 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 5 STR_VAR resource = ~d5poiz2a~ END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 321 target = 1 timing = 1 STR_VAR resource = d5poiz2a~~ END

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5poiz3a.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 25 target = 1 parameter1 = 3 parameter2 = 3 timing = 0 duration = 7 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 78 target = 1 parameter1 = 4 parameter2 = 4 timing = 0 duration = 7 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 5 STR_VAR resource = ~d5poiz3a~ END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5poiz3a~ END

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5poiz4a.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 25 target = 1 parameter1 = 6 parameter2 = 3 timing = 0 duration = 7 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 78 target = 1 parameter1 = 3 parameter2 = 4 timing = 0 duration = 7 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 5 STR_VAR resource = ~d5poiz4a~ END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5poiz4a~ END

// apply via 177/232 based on magnitude 

ACTION_PHP_EACH poison_effects AS res => mag BEGIN
  OUTER_SPRINT type ~%res_1%~
  ACTION_IF (FILE_EXISTS_IN_GAME ~%res%.%type%~) BEGIN
	COPY_EXISTING ~%res%.%type%~ ~override~
	  PATCH_IF (~%mag%~ STRING_EQUAL_CASE ~lethal~) BEGIN
		LPF DELETE_EFFECT INT_VAR match_opcode = 25 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 0 parameter2 = 2 timing = 0 duration = 180 STR_VAR resource = ~d5poiz1~ END
	  END
	  PATCH_IF (~%mag%~ STRING_EQUAL_CASE ~strong~) BEGIN
		LPF DELETE_EFFECT INT_VAR match_opcode = 25 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 0 parameter2 = 2 timing = 0 duration = 240 STR_VAR resource = ~d5poiz2~ END
	  END
	  PATCH_IF (~%mag%~ STRING_EQUAL_CASE ~moderate~) /* OR (~%mag%~ STRING_EQUAL_CASE ~12_over_time~) */ BEGIN
		LPF DELETE_EFFECT INT_VAR match_opcode = 25 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 0 parameter2 = 2 timing = 0 duration = 300 STR_VAR resource = ~d5poiz3~ END
	  END
	  PATCH_IF (~%mag%~ STRING_EQUAL_CASE ~weak~) OR (~%mag%~ STRING_EQUAL_CASE ~weaker~) BEGIN
		LPF DELETE_EFFECT INT_VAR match_opcode = 25 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 0 parameter2 = 2 timing = 0 duration = 300 STR_VAR resource = ~d5poiz4~ END
	  END
	BUT_ONLY
  END
END

// Slow Poison spell: apply 'slow poison' stat (for how long...?? 1 turn?)
// needs to identify all slow poison spells
//OUTER_SPRINT $slow_poison_spells(~spell~) ~1~
//OUTER_SPRINT $neutralize_poison_spells(~spell~) ~1~

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
 PATCH_IF (%SOURCE_SIZE% > 0x71) BEGIN
  SET slow = 0
  SET neutralize = 0
  GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
  PHP_EACH ab_array AS int => ab_off BEGIN
	GET_OFFSET_ARRAY2 fx_array ab_off SPL_V10_HEAD_EFFECTS
	PHP_EACH fx_array AS int => fx_off BEGIN
	  PATCH_IF slow = 0 BEGIN
		READ_SHORT fx_off fx_type
		PATCH_IF fx_type = 11 BEGIN
		  SET slow = 1
		END
		PATCH_IF fx_type = 79 BEGIN
		  SET neutralize = 1
		END
	  END
	END
  END
  PATCH_IF (slow = 1) && (neutralize = 0) BEGIN
	SPRINT $slow_poison_spells(~%SOURCE_RES%~) ~1~
  END
  PATCH_IF (slow = 1) && (neutralize = 1) BEGIN
	SPRINT $neutralize_poison_spells(~%SOURCE_RES%~) ~1~
  END
 END
BUT_ONLY

ACTION_PHP_EACH slow_poison_spells AS slow_spl => num BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%slow_spl%.spl~) BEGIN
	COPY_EXISTING ~%slow_spl%.spl~ ~override~
	  LPF DELETE_EFFECT INT_VAR match_opcode = 11 END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 233 target = 1 parameter1 = (1 << 24) parameter2 = (115 + (0x10000 * 1)) timing = 0 duration = 120 END
	BUT_ONLY
  END
END

ACTION_PHP_EACH neutralize_poison_spells AS neutralize_spl => num BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%neutralize_spl%.spl~) BEGIN
	COPY_EXISTING ~%neutralize_spl%.spl~ ~override~
	  LPF DELETE_EFFECT INT_VAR match_opcode = 11 END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 360 STR_VAR resource = ~d5poiz1~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 360 STR_VAR resource = ~d5poiz2~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 360 STR_VAR resource = ~d5poiz3~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 360 STR_VAR resource = ~d5poiz4~ END
	BUT_ONLY
  END
END

// Antidotes: apply 'slow poison' stat
COPY_EXISTING ~potn20.itm~ ~override~
  SAY IDENTIFIED_DESC ~As the name implies, this potion is used to combat any poison currently affecting the user. Mild poisons can be halted entirely by drinking this brew. More dangerous poisons, like that of wyverns and fiends, will continue to affect the drinker but the severity of their effects will be reduced.~
  LPF DELETE_EFFECT INT_VAR match_opcode = 11 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 233 target = 1 parameter1 = (1 << 24) parameter2 = (115 + (0x10000 * 1)) timing = 0 duration = 300 END
BUT_ONLY

// ***** maybe have the elixir actually neutralize...?
COPY_EXISTING ~potn17.itm~ ~override~
  SAY IDENTIFIED_DESC ~The Elixir of Health is a powerful healing mixture designed to cleanse the body of all impurities. First it cures the user of all diseases and poisons, then it restores 10 hit points to speed the recovery process.~
/*
  LPF DELETE_EFFECT INT_VAR match_opcode = 11 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 233 target = 1 parameter1 = (1 << 24) parameter2 = (115 + (0x10000 * 1)) timing = 0 duration = 120 END
*/
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 360 STR_VAR resource = ~d5poiz1~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 360 STR_VAR resource = ~d5poiz2~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 360 STR_VAR resource = ~d5poiz3~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 0 duration = 360 STR_VAR resource = ~d5poiz4~ END
BUT_ONLY

END